plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services' // To read the google-services.json config for FIREBASE
}

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "com.android.go4lunch"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resConfigs "en" // And any other languages you support
        vectorDrawables.useSupportLibrary = true // to use SVG

        // should correspond to key/value pairs inside the file
        //buildConfigField("String", "GOOGLE_PLACE_API_KEY", apikeyProperties['GOOGLE_PLACE_API_KEY'])

        def addConstant = {constantName, constantValue ->
            manifestPlaceholders += [ (constantName):constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }

        addConstant("GOOGLE_PLACE_API_KEY", apikeyProperties['GOOGLE_PLACE_API_KEY'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Nav drawer
    implementation 'androidx.navigation:navigation-ui:2.3.0'
    // Settings
    implementation 'androidx.preference:preference:1.1.1'
    // support for the new language APIs, cf. compile options
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.9'

    // For tests
    testImplementation 'junit:junit:4.+'
    testImplementation 'org.mockito:mockito-core:1.9.5'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // EASY PERMISSIONS
    implementation 'pub.devrel:easypermissions:3.0.0'

    // GOOGLE
    // Material
    implementation 'com.google.android.material:material:1.4.0'

    // Services for Maps, Location, Auth
    implementation 'com.google.android.gms:play-services-maps:18.0.1'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.android.gms:play-services-auth:19.2.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.2'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.3')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-core:9.6.1'

    // BUTTERKNIFE
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

    // GLIDE
    // Glide v4 uses annotation processor
    // see https://bumptech.github.io/glide/doc/generatedapi.html
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // RETROFIT
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // REACTIVE X
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'

    // FIREBASE-UI
    implementation 'com.firebaseui:firebase-ui-auth:8.0.0'

    // FACEBOOK - latest Facebook SDK releases here: https://github.com/facebook/facebook-android-sdk/blob/master/CHANGELOG.md
    implementation 'com.facebook.android:facebook-android-sdk:8.0.0'

    // If Facebook login support is required
    implementation 'com.facebook.android:facebook-login:8.1.0'

}